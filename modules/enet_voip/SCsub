# SCsub
Import('env')


import os.path


env_voip_enet = env.Clone()

# Thirdparty source files

#do not disable opus module. This module requires libopus
#if you want to disable audiostreamopus,
#enable the module but also enable stub
#https://github.com/godotengine/godot/blob/master/modules/opus/SCsub#L7
#assumes static linking

if env['builtin_opus']:
	thirdparty_dir = "#thirdparty/opus/"

	thirdparty_include_paths = [
		"",
		"celt",
		"opus",
		"silk",
		"silk/fixed",
		"silk/float",
	]
	env_voip_enet.Append(CPPPATH=[thirdparty_dir + "/" + dir for dir in thirdparty_include_paths])

	# also requires libogg
	if env['builtin_libogg']:
		env_voip_enet.Append(CPPPATH=["#thirdparty/libogg"])

env_voip_enet.Append(CPPPATH=["#modules/enet_voip/thirdparty_includes"])

voip_enet_dir = '#modules/enet_voip/'
flatbuffers_proto = 'godotvoip.fbs'

flatbuffers_suffix = ".fbs"

def _flatbuffers_emitter(target, source, env):
	target = []
	for s in source:
		src = str(s)
		if src.endswith(flatbuffers_suffix):
			p_stem = src[:-len(flatbuffers_suffix)]
		else:
			p_stem = src
		target.append(p_stem + '_generated.h')
	return target, source

def filter_headers(f_list):
	l = []
	for f in f_list:
		if os.path.splitext(f.rstr())[1] != '.h':
			l.append(f)
	return l	

bld = Builder(action="flatc --binary --natural-utf8 --cpp $SOURCE", \
#	suffix = '_generated.h', \
	src_suffix = [flatbuffers_suffix], \
	emitter = _flatbuffers_emitter)
env_voip_enet['BUILDERS']['Protoc'] = bld
flatbuffers_target = env_voip_enet.Protoc( flatbuffers_proto )
sources = Glob("*.cpp")+filter_headers(flatbuffers_target)
env_voip_enet.add_source_files(env.modules_sources, sources )
env_voip_enet.Append(CXXFLAGS=['-O2', '-std=c++14'])